buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
        mavenLocal()
        maven {
            credentials {
                username mavenUser
                password mavenPassword
            }
            url "https://nexus.causecode.com/repository/maven-releases/"
        }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.causecode.plugins:gradle-code-quality:1.0.0"
    }
}

version "0.0.9"
group "com.causecode.plugins"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin:"gradle-code-quality"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.springframework.boot:spring-boot-starter-tomcat"

    compile "org.grails:grails-core"
    compile "org.grails:grails-logging"

    compile "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-url-mappings"
    provided "org.grails:grails-plugin-domain-class"

    console "org.grails:grails-console"

    provided "org.grails.plugins:mongodb"
    provided "org.grails.plugins:embedded-mongodb:1.0.1"
    provided "org.grails.plugins:quartz:2.0.9"

    profile "org.grails.profiles:plugin"

    testCompile "org.grails:grails-plugin-testing"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId "mongo-update-embedded"

            pom.withXml {
                def pomNode = asNode()
                // version-less dependencies are handled with dependencyManagement
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username mavenPublishUser
                password mavenPublishPassword
            }
            url "https://nexus.causecode.com/repository/maven-releases/"
        }
    }
}

grailsPublish {
    // TODO: Provide values here
    user = 'user'
    key = 'key'
    githubSlug = 'foo/bar'
    license {
        name = 'Apache-2.0'
    }
    title = "My Plugin"
    desc = "Full plugin description"
    developers = [johndoe:"John Doe"]
    portalUser = ""
    portalPassword = ""
}

ccJacocoExt {
    excludes = [
            '**/*UpdateEmbeddedInstancesJob.*/**', '**/*Application.*/**'
    ]

    includeIntegrationTest = true
}

jar {
    exclude('test/**')
}

integrationTest {
    reports.html.enabled true
}

